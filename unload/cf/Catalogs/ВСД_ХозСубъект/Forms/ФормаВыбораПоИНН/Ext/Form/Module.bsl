
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СтруктураОтбора") Тогда
		Запрос = Новый Запрос;
		Запрос.Текст ="ВЫБРАТЬ
		              |	ВСД_ХозСубъект.Ссылка КАК Хозсубъект,
		              |	ВСД_ХозСубъект.Контрагент КАК Контрагент,
		              |	КОЛИЧЕСТВО(ВСД_Площадка.Ссылка) КАК КоличествоПривязанныхПлощадок,
		              |	ПОДСТРОКА(ВСД_ХозСубъект.ИНН, 0, 20) КАК ИНН,
		              |	ПОДСТРОКА(ВСД_ХозСубъект.Адрес, 0, 200) КАК Адрес
		              |ИЗ
		              |	Справочник.ВСД_ХозСубъект КАК ВСД_ХозСубъект
		              |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВСД_Площадка КАК ВСД_Площадка
		              |		ПО (ВСД_Площадка.ХозСубъект = ВСД_ХозСубъект.Ссылка)
		              |ГДЕ
		              |	ВСД_ХозСубъект.ИНН ПОДОБНО &ИНН
		              |	И НЕ ВСД_ХозСубъект.ПометкаУдаления
		              |
		              |СГРУППИРОВАТЬ ПО
		              |	ВСД_ХозСубъект.Ссылка,
		              |	ВСД_ХозСубъект.Контрагент,
		              |	ПОДСТРОКА(ВСД_ХозСубъект.ИНН, 0, 20),
		              |	ПОДСТРОКА(ВСД_ХозСубъект.Адрес, 0, 200)";
		Запрос.УстановитьПараметр("ИНН", Параметры.СтруктураОтбора.ИНН);
		Контрагент = Параметры.СтруктураОтбора.Контрагент;
		ХозсубъектыСписок.Загрузить( Запрос.Выполнить().Выгрузить() );
		Элементы.ПолеИнформация.Заголовок = "По ИНН """+Параметры.СтруктураОтбора.ИНН+""" 
				|уже загружен хозяйствующий субъект. Выберите к какому хозяйствующему субъекту привязать контрагента """
				+Параметры.СтруктураОтбора.Контрагент.Наименование+"""";

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ЗначениеЗаполнено(Контрагент) Тогда
		ВыбСтрока = Элементы.ХозсубъектыСписок.ТекущиеДанные;
		ИзменитьХозсубъектНаСервере(ВыбСтрока.ХозСубъект);	
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьХозсубъектНаСервере(ХозСубъектСсылка)
	
	Попытка
		ХозСубъектОбъект = ХозСубъектСсылка.ПолучитьОбъект();
		ХозСубъектОбъект.Контрагент = Контрагент;
		ХозСубъектОбъект.Записать();
		ТекстСообщения = "Установлена связь хозсубъекта """+ХозСубъектСсылка.Наименование+""" с контрагентом """+Контрагент.Наименование+""";";
	Исключение
		ТекстСообщения = ОписаниеОшибки();
	КонецПопытки;
	
	кб99_ВСД.СообщитьИнфо(ТекстСообщения, ХозСубъектСсылка);

КонецПроцедуры

