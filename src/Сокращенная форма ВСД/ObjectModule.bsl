Функция ПечатьДокумента()
	
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("Макет");
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.НомерДок = СсылкаНаОбъект.Номер;
	ОбластьМакета.Параметры.ДатаДок = Формат(СсылкаНаОбъект.Дата,"ДЛФ=Д");
	
	ТабДокумент.Вывести(ОбластьМакета);
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	ВСД_транзакция.Ссылка КАК Ссылка1,
	|	ВСД.Ссылка КАК ВСД
	|ИЗ
	|	Документ.ВСД КАК ВСД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВСД_транзакция КАК ВСД_транзакция
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|			ПО ВСД_транзакция.ДокументОснование = РеализацияТоваровУслуг.Ссылка
	|		ПО ВСД.ДокументОснование = ВСД_транзакция.Ссылка
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка = &Ссылка";
 
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект); //используем в качестве параметра реквизит обработки СсылкаНаОбъект
		
	ОбластьМакета = Макет.ПолучитьОбласть("ОбластьШтрихкод");
	
	Шапка = Запрос.Выполнить().Выбрать();
	Пока Шапка.Следующий() цикл
		докВСД = Шапка.ВСД;
		
		тВСД = "Ветеринарное свидетельство (оформлен) Код: "+докВСД.UUID;
		тОтправитель = "Фирма-отправитель: "+ СсылкаНаОбъект.Организация.НаименованиеПолное+", ИНН:"+ 
			СсылкаНаОбъект.Организация.ИНН+", ТТН: № "+СсылкаНаОбъект.Номер+" от "+СсылкаНаОбъект.Дата;
		тПолучатель = "Фирма-получатель: "+СсылкаНаОбъект.Контрагент.НаименованиеПолное+", ИНН: "+СсылкаНаОбъект.Контрагент.ИНН; 
		
		СведенияОГрузополучателе  = УправлениеКонтактнойИнформацией.СведенияОЮрФизЛице(докВСД.Получатель_Площадка.Контрагент,  докВСД.Дата);
		//тПлощадка = "Предприятие-получатель: "+ФормированиеПечатныхФормСервер.ОписаниеОрганизации(СведенияОГрузополучателе, "ПолноеНаименование,ИНН,ФактическийАдрес");;
		тПлощадка = "Предприятие-получатель: "+ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОГрузополучателе, "ПолноеНаименование,ИНН,ФактическийАдрес");;
		тПродукция = "Продукция: "+докВСД.НаименованиеПродукции +", "+докВСД.Количество+" кг";
		
		Если ЗначениеЗаполнено(докВСД.ДатаИзготовления1) Тогда
			тВыработано = "Выработана: "+Формат(докВСД.ДатаИзготовления1,"ДФ=dd.MM.yyyy; ДЛФ=D; ДП=-")+"-"+Формат(докВСД.ДатаИзготовления2,"ДФ=dd.MM.yyyy; ДЛФ=D; ДП=-");					
		Иначе
			тВыработано = "Выработана: " + Сокрлп(докВСД.ДатаИзготовления);
		КонецЕсли;
		Если ЗначениеЗаполнено(докВСД.ДатаСрокГодности1) Тогда
			тВыработано = тВыработано + 
			" срок годности: "+ Формат(докВСД.ДатаСрокГодности1,"ДФ=dd.MM.yyyy; ДЛФ=D; ДП=-")+"-"+Формат(докВСД.ДатаСрокГодности2,"ДФ=dd.MM.yyyy; ДЛФ=D; ДП=-");
		Иначе
			тВыработано = тВыработано + " срок годности: "+ СокрЛП(докВСД.ДатаСрокГодности);
		КонецЕсли;
		
		тКод = "Код: "+докВСД.UUID;  
		тСгенерировано = "Сгенерировано системой '1С' "+ТекущаяДата()+" "+ПользователиИнформационнойБазы.ТекущийПользователь().ПолноеИмя;
	   			 			
		шк = "http://mercury.vetrf.ru/pub/operatorui?_language=ru&_action=showVetDocumentFormByUuid&uuid= "+докВСД.UUID;	
		
		ОбластьМакета.Рисунки.Штрихкод.Объект.symbology = 58; // тип = QR
	    ОбластьМакета.Рисунки.Штрихкод.Объект.barcode = шк; 
		ОбластьМакета.Параметры.тВСД = тВСД;
		ОбластьМакета.Параметры.тОтправитель = тОтправитель;
		ОбластьМакета.Параметры.тПолучатель = тПолучатель;
		ОбластьМакета.Параметры.тПлощадка = тПлощадка;
		ОбластьМакета.Параметры.тПродукция = тПродукция;
		ОбластьМакета.Параметры.тВыработано = тВыработано;
		ОбластьМакета.Параметры.тКод = тКод;
		ОбластьМакета.Параметры.тСгенерировано = тСгенерировано;
		
		ТабДокумент.Вывести(ОбластьМакета);
	КонецЦикла;

	
	ТабДокумент.АвтоМасштаб = Истина;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДокумент.ИмяПараметровПечати = "Удостоверение качества";
	
	
	Возврат ТабДокумент;
	
 
КонецФункции // ПечатьДокумента()

// Функция является точкой входа в данную обработку.
// Алгоритм заполнения печатной формы удобно размещать в отдельной(ых) функции(ях)
// Возвращаемое значение:
// Табличный документ - печатная форма накладной
//
Функция Печать() Экспорт
	
	ТабДокумент = ПечатьДокумента();
	Возврат ТабДокумент;
	
КонецФункции // Печать
	
